SET QUOTED_IDENTIFIER ON
GO
SET ANSI_NULLS ON
GO
CREATE   FUNCTION [Logs].[Fault_Item_Per_Day](@FromDay DATE = NULL)
RETURNS @FAULT_VALIDI_ITEM TABLE
(
	ID				INT,
	ALARM_ID		INT,
	GIORNO			DATETIME,
	ITEM			VARCHAR(MAX),
	DESCRIZIONE		VARCHAR(MAX),
	ELAPSED_TIME	NUMERIC(10,3),
	TEMPO_DI_LAVORO FLOAT
)
AS
BEGIN
	DECLARE @FAULT_ASI TABLE
	(
		ID				INT,
		ITEM			VARCHAR(MAX),
		RAISED_TIME		DATETIME,
		RESETTED_TIME	DATETIME,
		DESCRIZIONE		VARCHAR(MAX),
		TEMPO_DI_LAVORO	FLOAT
	)
	INSERT INTO @FAULT_ASI
	SELECT	A.ID,
			T.ASI,
			A.RaisedTimeStamp,
			A.ResettedTimeStamp,
			A.Description,
			DATEDIFF(SECOND,IFG.StartDateTime,IFG.EndDateTime)/60.00
	FROM	dbo.TAG		T
	JOIN	LOGS.Alarms	A
	ON		A.TagId = T.TAGID
		AND	A.ResettedTimeStamp IS NOT NULL
		AND T.TAGTYPE IN ('FaultMsg')
		AND CAST(A.RaisedTimeStamp AS DATE) = CAST(A.ResettedTimeStamp AS DATE)
		AND CAST(A.RaisedTimeStamp AS DATE) >= ISNULL(@FromDay,CAST(A.RaisedTimeStamp AS DATE))
	JOIN	LOGS.Inizio_Fine_Giorno(@FromDay)		IFG
	ON		A.RaisedTimeStamp >= IFG.StartDateTime
		AND	A.ResettedTimeStamp <= IFG.EndDateTime
	LEFT
	JOIN	LOGS.Allarmi_Da_Escludere()	ADE
	ON		ADE.Alarm_Id = A.Id
	WHERE	A.Description NOT LIKE '%STATO DI ERRORE%'
		AND ADE.Alarm_Id IS NULL

	OPTION(RECOMPILE)
		
	DECLARE @FAULT_ASI_VALIDI TABLE
	(
		ID				INT,
		ALARMID			INT,
		ITEM			VARCHAR(MAX),
		RAISED_TIME		DATETIME,
		RESETTED_TIME	DATETIME,
		DESCRIZIONE		VARCHAR(MAX),
		TEMPO_DI_LAVORO	FLOAT
	)
	INSERT INTO @FAULT_ASI_VALIDI
	SELECT	ROW_NUMBER() OVER (ORDER BY ITEM, RAISED_TIME),
			FA.ID,
			FA.ITEM,
			FA.RAISED_TIME,
			FA.RESETTED_TIME,
			FA.DESCRIZIONE,
			FA.TEMPO_DI_LAVORO
	FROM	@FAULT_ASI			FA
	WHERE	NOT EXISTS
			(
				SELECT TOP (1) 1 
				FROM @FAULT_ASI FA2
				WHERE FA2.ID <> FA.ID
					AND FA2.RAISED_TIME < FA.RAISED_TIME 
					AND FA2.RESETTED_TIME > FA.RESETTED_TIME
					AND FA.ITEM = FA2.ITEM
			)
	OPTION (RECOMPILE)

	INSERT INTO @FAULT_VALIDI_ITEM
	(
		ID,
		ALARM_ID,
		GIORNO,
		ITEM,
		DESCRIZIONE,
		ELAPSED_TIME,
		TEMPO_DI_LAVORO
	)
	SELECT	FAV.ID,
			FAV.ALARMID,	
			CAST(FAV.RAISED_TIME AS DATE)								GIORNO,
			FAV.ITEM													ITEM,
			FAV.DESCRIZIONE,
			DATEDIFF(SECOND,FAV.RAISED_TIME,FAV.RESETTED_TIME)/60.00	Minuti_Per_Ripristino,
			FAV.TEMPO_DI_LAVORO
	FROM	@FAULT_ASI_VALIDI			FAV
	LEFT
	JOIN	@FAULT_ASI_VALIDI			FAV_2
	ON		FAV_2.Id = FAV.Id - 1
	WHERE	FAV.ITEM IS NOT NULL
		AND
		(
			ISNULL(FAV_2.ITEM,'') <> FAV.ITEM
			OR
			FAV.ALARMID = FAV_2.ALARMID
			OR
			(
				FAV.RAISED_TIME > FAV_2.RESETTED_TIME
				AND
				FAV_2.ITEM = FAV.ITEM
			)
		)
	OPTION (RECOMPILE)

	RETURN
END






GO
